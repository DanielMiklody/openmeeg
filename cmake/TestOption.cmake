#-----------------------------------------------
# tests
#-----------------------------------------------

option(BUILD_TESTING "Build tests" OFF)

if (BUILD_TESTING)
    if (WIN32 OR APPLE)
        set(lib_separator ":")
        if (WIN32)
            set(lib_separator ";")
        endif()
        set(TestConfigFile "${CMAKE_BINARY_DIR}/TestConfig.cmake")
        get_property(HDF5_LIB TARGET hdf5 PROPERTY LOCATION)
        get_filename_component(HDF5_BIN_DIR ${HDF5_DIR} DIRECTORY)
        set(INSTALL_DIRS "${ZLIB_ROOT}/bin" "${HDF5_ROOT_DIR}/bin" "${HDF5_BIN_DIR}" "${matio_ROOT_DIR}/bin"
                         "${LAPACK_DLL_DIR}" "${VTK_LIBRARY_DIRS}" "${CGAL_LIBRARY_DIRS}" "${NIFTI_DIR}")
        foreach (dir ${INSTALL_DIRS})
            set(LIBRARY_PATHS "${dir}${lib_separator}${LIBRARY_PATHS}")
        endforeach()
        if (WIN32)
            file(WRITE ${TestConfigFile} "set(ENV{PATH} \"${LIBRARY_PATHS}\$ENV{PATH}\")\n")
        else()
            file(WRITE ${TestConfigFile} "set(ENV{DYLD_LIBRARY_PATH} \"${LIBRARY_PATHS}\$ENV{DYLD_LIBRARY_PATH}\")\n")
        endif()
        set_directory_properties(PROPERTIES TEST_INCLUDE_FILE "${TestConfigFile}")
    endif()

    set(CTEST_BUILD_NAME "${CMAKE_SYSTEM_NAME}-${CMAKE_SYSTEM_PROCESSOR}")
    if (USE_MKL)
        set(CTEST_BUILD_NAME "${CTEST_BUILD_NAME}-MKL")
    elseif (USE_ATLAS)
        set(CTEST_BUILD_NAME "${CTEST_BUILD_NAME}-Atlas")
    else()
        set(CTEST_BUILD_NAME "${CTEST_BUILD_NAME}-BlasLapack")
    endif()
    if (BUILD_SHARED_LIBS)
        set(CTEST_BUILD_NAME "${CTEST_BUILD_NAME}-shared")
    else()
        set(CTEST_BUILD_NAME "${CTEST_BUILD_NAME}-static")
    endif()
    set(BUILDNAME ${CTEST_BUILD_NAME})
    include(CTest)
    enable_testing()
    mark_as_advanced(BUILD_TESTING)
    subdirs(tests)
endif()

if (USE_GCC AND BUILD_TESTING)
    option(ENABLE_COVERAGE "Enable coverage" OFF)
    mark_as_advanced(ENABLE_COVERAGE)
endif()

if (ENABLE_COVERAGE)
    if (USE_GCC)
        set(COVERAGE_FLAGS "-fprofile-arcs -ftest-coverage")
        set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${COVERAGE_FLAGS} -lgcov")
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${COVERAGE_FLAGS}")
        set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${COVERAGE_FLAGS}")
    else()
        message(SEND_ERROR "Coverage is only available with gcc.")
    endif()
endif()
